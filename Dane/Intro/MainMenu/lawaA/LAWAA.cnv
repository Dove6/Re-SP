OBJECT=IREXSTONEPOS
IREXSTONEPOS:TYPE=INTEGER
IREXSTONEPOS:VALUE=0

OBJECT=IREXSTONEPOSPREV
IREXSTONEPOSPREV:TYPE=INTEGER
IREXSTONEPOSPREV:VALUE=0

OBJECT=IFINISHID
IFINISHID:TYPE=INTEGER
IFINISHID:VALUE=10

OBJECT=BWALKINGIN
BWALKINGIN:TYPE=BOOL
BWALKINGIN:VALUE=TRUE

OBJECT=SSTAGEID
SSTAGEID:TYPE=STRING
SSTAGEID:VALUE=A


######################################
##############  ARRAY  ###############
######################################

OBJECT=ARRPATTERN
ARRPATTERN:TYPE=ARRAY

OBJECT=ARRTRANSITIONS
ARRTRANSITIONS:TYPE=MULTIARRAY
ARRTRANSITIONS:DIMENSIONS=2
# 1. ORIGIN, 2. DESTINATION

OBJECT=ARRFALLING
ARRFALLING:TYPE=MULTIARRAY
ARRFALLING:DIMENSIONS=2
# 1. CLONE INDEX, 2. DATA INDEX (0 for STONE INDEX, 1 for PAUSE LENGTH)


######################################
##############  STRUCT  ##############
######################################

OBJECT=STRREXPIXELPOSORIG
STRREXPIXELPOSORIG:TYPE=STRUCT
STRREXPIXELPOSORIG:FIELDS=X<INTEGER>,Y<INTEGER>

OBJECT=STRREXPIXELPOSDEST
STRREXPIXELPOSDEST:TYPE=STRUCT
STRREXPIXELPOSDEST:FIELDS=X<INTEGER>,Y<INTEGER>

OBJECT=STRSTONEDATA
STRSTONEDATA:TYPE=STRUCT
STRSTONEDATA:FIELDS=POSX<INTEGER>,POSY<INTEGER>,POSZ<INTEGER>,INITEVT<STRING>,HASFALL<BOOL>,FALLWAIT<INTEGER>

OBJECT=STRTRANSITION
STRTRANSITION:TYPE=STRUCT
STRTRANSITION:FIELDS=ORIG<INTEGER>,DEST<INTEGER>,DIRECTION<STRING>


######################################
#############  DATABASE  #############
######################################

OBJECT=DTBSTONEDATA
DTBSTONEDATA:TYPE=DATABASE
DTBSTONEDATA:MODEL=STRSTONEDATA

OBJECT=DTBTRANSITIONS
DTBTRANSITIONS:TYPE=DATABASE
DTBTRANSITIONS:MODEL=STRTRANSITION


######################################
##############  ANIMO  ###############
######################################

OBJECT=ANNBUBBLE
ANNBUBBLE:TYPE=ANIMO
ANNBUBBLE:VISIBLE=FALSE
ANNBUBBLE:TOCANVAS=TRUE
ANNBUBBLE:PRIORITY=0
ANNBUBBLE:PRELOAD=TRUE
ANNBUBBLE:ONFINISHED={THIS^PLAY("PLAY");}

OBJECT=ANNSTONE
ANNSTONE:TYPE=ANIMO
ANNSTONE:FILENAME=STONE.ANN
ANNSTONE:VISIBLE=FALSE
ANNSTONE:TOCANVAS=TRUE
ANNSTONE:PRIORITY=1
ANNSTONE:PRELOAD=TRUE
ANNSTONE:ONFINISHED^ZAPADA={THIS^PLAY("WYNURZA");}

OBJECT=ANNREX
ANNREX:TYPE=ANIMO
ANNREX:VISIBLE=TRUE
ANNREX:FILENAME=REKSIO18.ANN
ANNREX:TOCANVAS=TRUE
ANNREX:PRIORITY=8
ANNREX:PRELOAD=TRUE
ANNREX:ONFINISHED^ZPLANSZY17={BEHENABLEBUTTONS^RUN();}
ANNREX:ONFINISHED^PRZYPALONY={BEHCHANGESTAGE^RUN("A");}
ANNREX:ONFINISHED^TRAFIONY={BEHCHANGESTAGE^RUN("A");}
ANNREX:ONSTARTED^SPADA={@IF("VARLAWAZYCIA'1","{SNDHINT^PLAY();VARLAWAZYCIA^SET(5);}","");}
ANNREX:ONFINISHED^SPADA={VARLAWA^SET(0);BEHENABLEBUTTONS^RUN();}
ANNREX:ONFINISHED^E=BEHJUMPEND
ANNREX:ONFINISHED^W=BEHJUMPEND
ANNREX:ONFINISHED^NE=BEHJUMPEND
ANNREX:ONFINISHED^NW=BEHJUMPEND
ANNREX:ONFINISHED^SE=BEHJUMPEND
ANNREX:ONFINISHED^SW=BEHJUMPEND
ANNREX:ONFRAMECHANGED^E=BEHJUMPING
ANNREX:ONFRAMECHANGED^W=BEHJUMPING
ANNREX:ONFRAMECHANGED^NE=BEHJUMPING
ANNREX:ONFRAMECHANGED^NW=BEHJUMPING
ANNREX:ONFRAMECHANGED^SE=BEHJUMPING
ANNREX:ONFRAMECHANGED^SW=BEHJUMPING
ANNREX:ONFINISHED^OGLADAMEPE={RATUNEK^GOTO("MAPAPELNA");}
ANNREX:ONFINISHED^DOPLANSZY17={RATUNEK^GOTO("CIEMNOSC");}
ANNREX:ONFINISHED^DOPLANSZY21={THIS^HIDE();@IF("SSTAGEID'"C"","{ROZDZIAL6^RESETINI();ROZDZIAL7^RESETINI();RATUNEK^GOTO("21MOST");}","");BWALKINGIN^SET(TRUE);@IF("SSTAGEID'"B"","{BEHCHANGESTAGE^RUN("C");}","");@IF("SSTAGEID'"A"","{BEHCHANGESTAGE^RUN("B");}","");}

OBJECT=ANNFALLINGROCK
ANNFALLINGROCK:TYPE=ANIMO
ANNFALLINGROCK:FILENAME=FALLING.ANN
ANNFALLINGROCK:VISIBLE=TRUE
ANNFALLINGROCK:TOCANVAS=TRUE
ANNFALLINGROCK:PRIORITY=9
ANNFALLINGROCK:PRELOAD=TRUE
ANNFALLINGROCK:ONFINISHED^SPADA={THIS^PLAY("END");}
ANNFALLINGROCK:ONSTARTED^END={@INT("L_ICLONEINDEX",THIS^GETCLONEINDEX());@IF("IREXSTONEPOS'ARRFALLING^GET(L_ICLONEINDEX,0)","BEHREXCRUSHING","");}
ANNFALLINGROCK:ONFINISHED^END={THIS^PLAY("PAUSE");}
ANNFALLINGROCK:ONSTARTED^PAUSE={THIS^HIDE();}
ANNFALLINGROCK:ONFRAMECHANGED^PAUSE={@INT("L_ICLONEINDEX",THIS^GETCLONEINDEX());@INT("L_IFRAMEINDEX",THIS^GETCFRAMEINEVENT());@IF("ARRFALLING^GET(L_ICLONEINDEX,1)'L_IFRAMEINDEX","{*["ANNFALLINGROCK_"+L_ICLONEINDEX]^STOP(TRUE);}","");}
ANNFALLINGROCK:ONFINISHED^PAUSE={THIS^PLAY("SPADA");}
# REMINDER: THERE ARE 150 FRAMES IN THE PAUSE EVENT, SO ANY LONGER INTERVAL REQUIRES TAMPERING EITHER WITH CONTENTS OF THE ANIMATION FILE OR ITS FRAME RATE HERE


######################################
##############  IMAGE  ###############
######################################

OBJECT=IMGOVERLAY
IMGOVERLAY:TYPE=IMAGE
IMGOVERLAY:VISIBLE=TRUE
IMGOVERLAY:FILENAME=OVERLAYA.IMG
IMGOVERLAY:TOCANVAS=TRUE
IMGOVERLAY:PRIORITY=10
IMGOVERLAY:PRELOAD=TRUE

OBJECT=IMGGOTO17
IMGGOTO17:TYPE=IMAGE
IMGGOTO17:VISIBLE=TRUE
IMGGOTO17:FILENAME=LAWAAGOTO17.IMG
IMGGOTO17:TOCANVAS=TRUE
IMGGOTO17:PRIORITY=11
IMGGOTO17:PRELOAD=TRUE

OBJECT=IMGMAPOVERSTD
IMGMAPOVERSTD:TYPE=IMAGE
IMGMAPOVERSTD:VISIBLE=TRUE
IMGMAPOVERSTD:FILENAME=BMAPA.IMG
IMGMAPOVERSTD:TOCANVAS=TRUE
IMGMAPOVERSTD:PRIORITY=20
IMGMAPOVERSTD:PRELOAD=TRUE

OBJECT=IMGMAPOVEROC
IMGMAPOVEROC:TYPE=IMAGE
IMGMAPOVEROC:VISIBLE=TRUE
IMGMAPOVEROC:FILENAME=BMAPAOC.IMG
IMGMAPOVEROC:TOCANVAS=TRUE
IMGMAPOVEROC:PRIORITY=20
IMGMAPOVEROC:PRELOAD=TRUE

OBJECT=IMGMAPOVEROM
IMGMAPOVEROM:TYPE=IMAGE
IMGMAPOVEROM:VISIBLE=TRUE
IMGMAPOVEROM:FILENAME=BMAPAOM.IMG
IMGMAPOVEROM:TOCANVAS=TRUE
IMGMAPOVEROM:PRIORITY=20
IMGMAPOVEROM:PRELOAD=TRUE

OBJECT=IMGMAPUNDERSTD
IMGMAPUNDERSTD:TYPE=IMAGE
IMGMAPUNDERSTD:VISIBLE=TRUE
IMGMAPUNDERSTD:FILENAME=BMAPA2.IMG
IMGMAPUNDERSTD:TOCANVAS=TRUE
IMGMAPUNDERSTD:PRIORITY=20
IMGMAPUNDERSTD:PRELOAD=TRUE

OBJECT=IMGMAPUNDEROC
IMGMAPUNDEROC:TYPE=IMAGE
IMGMAPUNDEROC:VISIBLE=TRUE
IMGMAPUNDEROC:FILENAME=BMAPA2OC.IMG
IMGMAPUNDEROC:TOCANVAS=TRUE
IMGMAPUNDEROC:PRIORITY=20
IMGMAPUNDEROC:PRELOAD=TRUE

OBJECT=IMGMAPUNDEROM
IMGMAPUNDEROM:TYPE=IMAGE
IMGMAPUNDEROM:VISIBLE=TRUE
IMGMAPUNDEROM:FILENAME=BMAPA2OM.IMG
IMGMAPUNDEROM:TOCANVAS=TRUE
IMGMAPUNDEROM:PRIORITY=20
IMGMAPUNDEROM:PRELOAD=TRUE

OBJECT=IMGMENUSTD
IMGMENUSTD:TYPE=IMAGE
IMGMENUSTD:VISIBLE=TRUE
IMGMENUSTD:FILENAME=BEXIT.IMG
IMGMENUSTD:TOCANVAS=TRUE
IMGMENUSTD:PRIORITY=20
IMGMENUSTD:PRELOAD=TRUE

OBJECT=IMGMENUOC
IMGMENUOC:TYPE=IMAGE
IMGMENUOC:VISIBLE=TRUE
IMGMENUOC:FILENAME=BEXITOC.IMG
IMGMENUOC:TOCANVAS=TRUE
IMGMENUOC:PRIORITY=20
IMGMENUOC:PRELOAD=TRUE

OBJECT=IMGMENUOM
IMGMENUOM:TYPE=IMAGE
IMGMENUOM:VISIBLE=TRUE
IMGMENUOM:FILENAME=BEXITOM.IMG
IMGMENUOM:TOCANVAS=TRUE
IMGMENUOM:PRIORITY=20
IMGMENUOM:PRELOAD=TRUE


######################################
##############  BUTTON  ##############
######################################

OBJECT=BTNSTONE
BTNSTONE:TYPE=BUTTON
BTNSTONE:VISIBLE=TRUE
BTNSTONE:ENABLE=TRUE
BTNSTONE:ONACTION={@INT("L_ICLONEINDEX",THIS^GETCLONEINDEX());BEHJUMPSTART^RUN(L_ICLONEINDEX);}

OBJECT=BTNGOTO17
BTNGOTO17:TYPE=BUTTON
BTNGOTO17:VISIBLE=TRUE
BTNGOTO17:ENABLE=TRUE
BTNGOTO17:RECT=0,280,80,530
BTNGOTO17:GFXONMOVE=IMGGOTO17
BTNGOTO17:ONACTION={@IF("IREXSTONEPOS'0","{BEHDISABLEBUTTONS^RUN();ANNREX^PLAY("DOPLANSZY17");}","");}

OBJECT=BTNFINISH
BTNFINISH:TYPE=BUTTON
BTNFINISH:VISIBLE=TRUE
BTNFINISH:ENABLE=TRUE
BTNFINISH:PRIORITY=1
BTNFINISH:RECT=710,470,800,600
BTNFINISH:ONACTION={BEHJUMPSTART^RUN(IFINISHID);}

OBJECT=BTNMAPOVER
BTNMAPOVER:TYPE=BUTTON
BTNMAPOVER:VISIBLE=TRUE
BTNMAPOVER:ENABLE=TRUE
BTNMAPOVER:GFXSTANDARD=IMGMAPOVERSTD
BTNMAPOVER:GFXONCLICK=IMGMAPOVEROC
BTNMAPOVER:GFXONMOVE=IMGMAPOVEROM
BTNMAPOVER:ONACTION={@IF("SSTAGEID'"A"&&IREXSTONEPOS'0","{BEHDISABLEBUTTONS^RUN();ANNREX^PLAY("OGLADAMEPE");}","{SNDMAPBLOCK^PLAY();}");}

OBJECT=BTNMAPUNDER
BTNMAPUNDER:TYPE=BUTTON
BTNMAPUNDER:VISIBLE=TRUE
BTNMAPUNDER:ENABLE=TRUE
BTNMAPUNDER:GFXSTANDARD=IMGMAPUNDERSTD
BTNMAPUNDER:GFXONCLICK=IMGMAPUNDEROC
BTNMAPUNDER:GFXONMOVE=IMGMAPUNDEROM
BTNMAPUNDER:ONACTION={@IF("SSTAGEID'"A"&&IREXSTONEPOS'0","{BEHDISABLEBUTTONS^RUN();RATUNEK^GOTO("MAPAPODZIEMI");}","{SNDMAPBLOCK^PLAY();}");}

OBJECT=BTNMENU
BTNMENU:TYPE=BUTTON
BTNMENU:VISIBLE=TRUE
BTNMENU:ENABLE=TRUE
BTNMENU:GFXSTANDARD=IMGMENUSTD
BTNMENU:GFXONCLICK=IMGMENUOC
BTNMENU:GFXONMOVE=IMGMENUOM
BTNMENU:ONACTION={RATUNEK^GOTO("MENUGLOWNE");}


######################################
##############  SOUND  ###############
######################################

OBJECT=SNDSUCCESS
SNDSUCCESS:TYPE=SOUND
SNDSUCCESS:FILENAME=18SUCCESSA.WAV
SNDSUCCESS:PRELOAD=TRUE
SNDSUCCESS:ONFINISHED={ANNREX^PLAY("DOPLANSZY21");}

OBJECT=SNDHINT
SNDHINT:TYPE=SOUND
SNDHINT:FILENAME=18HINT.WAV
SNDHINT:PRELOAD=TRUE

OBJECT=SNDMAPBLOCK
SNDMAPBLOCK:TYPE=SOUND
SNDMAPBLOCK:FILENAME=NOWE10.WAV
SNDMAPBLOCK:PRELOAD=TRUE


######################################
###############  MISC  ###############
######################################

OBJECT=CANVASOBSERVER
CANVASOBSERVER:TYPE=CANVAS_OBSERVER


######################################
############  BEHAVIOUR  #############
######################################

OBJECT=BEHENTRYFROMUI
BEHENTRYFROMUI:TYPE=BEHAVIOUR
BEHENTRYFROMUI:CODE={@IF("POZYCJAREKSIA'101","{ANNREX^PLAY("ODKLADAMAPE");}","{ANNREX^SETFRAME("DOPLANSZY17",0);}");}

OBJECT=BEHENTRY
BEHENTRY:TYPE=BEHAVIOUR
BEHENTRY:CODE={BEHDISABLEBUTTONS^RUN();@IF("BWALKINGIN'TRUE","{ANNREX^PLAY("ZPLANSZY17"); !IN_CASE_REX_WOULD_TAKE_TOO_LONG_TO_APPEAR; @INT("L_IINITFRAME",[12-STRREXPIXELPOSDEST|X]);L_IINITFRAME^MUL(ANNREX^GETNOFINEVENT("ZPLANSZY17"));L_IINITFRAME^DIV(100);L_IINITFRAME^CLAMP(0,[ANNREX^GETNOFINEVENT("ZPLANSZY17")-1]);ANNREX^SETFRAME("ZPLANSZY17",L_IINITFRAME);}","{ANNREX^PLAY("SPADA");}");}

OBJECT=BEHSTARTGAME
BEHSTARTGAME:TYPE=BEHAVIOUR
BEHSTARTGAME:CODE={DTBSTONEDATA^SELECT(0);STRREXPIXELPOSORIG|X^SET(0);STRREXPIXELPOSORIG|Y^SET(0);STRREXPIXELPOSDEST|X^SET(DTBSTONEDATA_CURSOR|POSX);STRREXPIXELPOSDEST|Y^SET(DTBSTONEDATA_CURSOR|POSY);IREXSTONEPOS^SET(0);IREXSTONEPOSPREV^SET(0);ANNREX^SETPOSITION(STRREXPIXELPOSDEST|X,STRREXPIXELPOSDEST|Y);@IF("POZYCJAREKSIA'100||POZYCJAREKSIA'101||POZYCJAREKSIA'102","BEHENTRYFROMUI","BEHENTRY");BWALKINGIN^SET(FALSE);}

OBJECT=BEHENABLEBUTTONS
BEHENABLEBUTTONS:TYPE=BEHAVIOUR
BEHENABLEBUTTONS:CODE={@LOOP("{*["BTNSTONE_"+_I_]^ENABLE();}",1,[DTBSTONEDATA^GETROWSNO()-2],1);@IF("SSTAGEID'"A"&&IREXSTONEPOS'0","{BTNGOTO17^ENABLE();}","");BTNFINISH^ENABLE();BTNMAPOVER^ENABLE();BTNMAPUNDER^ENABLE();}

OBJECT=BEHDISABLEBUTTONS
BEHDISABLEBUTTONS:TYPE=BEHAVIOUR
BEHDISABLEBUTTONS:CODE={@LOOP("{*["BTNSTONE_"+_I_]^DISABLEBUTVISIBLE();}",1,[DTBSTONEDATA^GETROWSNO()-2],1);BTNGOTO17^DISABLEBUTVISIBLE();BTNFINISH^DISABLEBUTVISIBLE();BTNMAPOVER^DISABLEBUTVISIBLE();BTNMAPUNDER^DISABLEBUTVISIBLE();}

# ARGUMENTS: $1 - next stone ID
OBJECT=BEHJUMPSTART
BEHJUMPSTART:TYPE=BEHAVIOUR
BEHJUMPSTART:CODE={@STRING("L_SDIRECTION",ARRTRANSITIONS^GET(IREXSTONEPOS,$1));@IF("L_SDIRECTION!'"NULL"","{DTBSTONEDATA^SELECT($1);STRREXPIXELPOSORIG^SET("STRREXPIXELPOSDEST");STRSTONEDATA^SET("DTBSTONEDATA_CURSOR");STRREXPIXELPOSDEST|X^SET(STRSTONEDATA|POSX);STRREXPIXELPOSDEST|Y^SET(STRSTONEDATA|POSY);IREXSTONEPOSPREV^SET(IREXSTONEPOS);IREXSTONEPOS^SET($1);BEHDISABLEBUTTONS^RUN();ANNREX^PLAY(L_SDIRECTION);}","");}

OBJECT=BEHJUMPING
BEHJUMPING:TYPE=BEHAVIOUR
BEHJUMPING:CODE={@INT("L_IPOSX",[STRREXPIXELPOSDEST|X-STRREXPIXELPOSORIG|X]);L_IPOSX^MUL([ANNREX^GETCFRAMEINEVENT()+1]);L_IPOSX^DIV(THIS^GETNOFINEVENT(THIS^GETEVENTNAME()));@INT("L_IPOSY",[STRREXPIXELPOSDEST|Y-STRREXPIXELPOSORIG|Y]);L_IPOSY^MUL([ANNREX^GETCFRAMEINEVENT()+1]);L_IPOSY^DIV(THIS^GETNOFINEVENT(THIS^GETEVENTNAME()));THIS^SETPOSITION([STRREXPIXELPOSORIG|X+L_IPOSX],[STRREXPIXELPOSORIG|Y+L_IPOSY]);}

OBJECT=BEHJUMPEND
BEHJUMPEND:TYPE=BEHAVIOUR
BEHJUMPEND:CODE={BEHENABLEBUTTONS^RUN();@BOOL("L_BPATTERNOK",TRUE);@INT("L_IPATTERNPOS1",ARRPATTERN^FIND(IREXSTONEPOSPREV));@INT("L_IPATTERNPOS2",ARRPATTERN^FIND(IREXSTONEPOS));@INT("L_IPATTERNDIFF",[L_IPATTERNPOS1-L_IPATTERNPOS2]);@IF("L_IPATTERNPOS1<0||L_IPATTERNPOS2<0","{L_BPATTERNOK^SET(FALSE);}","{L_IPATTERNDIFF^ABS(L_IPATTERNDIFF);}");@IF("L_IPATTERNDIFF>1","{L_BPATTERNOK^SET(FALSE);}","");@IF("L_BPATTERNOK'FALSE","BEHREXDEEPFRYING","");@IF("IREXSTONEPOS'IFINISHID","{BEHDISABLEBUTTONS^RUN();SNDSUCCESS^PLAY();}","");}

OBJECT=BEHREXDEEPFRYING
BEHREXDEEPFRYING:TYPE=BEHAVIOUR
BEHREXDEEPFRYING:CODE={VARLAWA^SET(1);BEHDISABLEBUTTONS^RUN();*["ANNSTONE_"+IREXSTONEPOS]^PLAY("ZAPADA");ANNREX^PLAY("PRZYPALONY");IREXSTONEPOS^SET(0);IREXSTONEPOSPREV^SET(0);VARLAWAZYCIA^DEC();}

OBJECT=BEHREXCRUSHING
BEHREXCRUSHING:TYPE=BEHAVIOUR
BEHREXCRUSHING:CODE={VARLAWA^SET(1);BEHDISABLEBUTTONS^RUN();ANNREX^PLAY("TRAFIONY");IREXSTONEPOS^SET(0);IREXSTONEPOSPREV^SET(0);VARLAWAZYCIA^DEC();}

OBJECT=BEHINITARRAYS
BEHINITARRAYS:TYPE=BEHAVIOUR
BEHINITARRAYS:CODE={!CLEARING; ARRPATTERN^REMOVEALL();DTBTRANSITIONS^SELECT(0);@LOOP("{STRTRANSITION^SET("DTBTRANSITIONS_CURSOR");ARRTRANSITIONS^SET(STRTRANSITION|ORIG,STRTRANSITION|DEST,NULL);DTBTRANSITIONS^NEXT();}",1,DTBTRANSITIONS^GETROWSNO(),1);DTBTRANSITIONS^REMOVEALL(); !FILLING; ARRPATTERN^LOAD(["PATTERN"+SSTAGEID+".ARR"]);IFINISHID^SET(ARRPATTERN^GET([ARRPATTERN^GETSIZE()-1]));DTBTRANSITIONS^LOAD(["TRANSITIONS"+SSTAGEID+".DTA"]);DTBTRANSITIONS^SELECT(0);@LOOP("{STRTRANSITION^SET("DTBTRANSITIONS_CURSOR");ARRTRANSITIONS^SET(STRTRANSITION|ORIG,STRTRANSITION|DEST,STRTRANSITION|DIRECTION);DTBTRANSITIONS^NEXT();}",1,DTBTRANSITIONS^GETROWSNO(),1);}

OBJECT=BEHGENERATESTONES
BEHGENERATESTONES:TYPE=BEHAVIOUR
BEHGENERATESTONES:CODE={!GETTING_RID_OF_LEFTOVERS; @LOOP("{*["ANNSTONE_"+_I_]^STOP(FALSE);*["BTNSTONE_"+_I_]^DISABLE();}",1,BTNSTONE^GETCLONESNO(),1);@LOOP("{*["ANNFALLINGROCK_"+_I_]^STOP(FALSE);*["ANNFALLINGROCK_"+_I_]^HIDE();}",1,ANNFALLINGROCK^GETCLONESNO(),1);DTBSTONEDATA^REMOVEALL(); !PREREQUISITES; DTBSTONEDATA^LOAD(["STONES"+SSTAGEID+".DTA"]);@INT("L_ISTONECOUNT",[DTBSTONEDATA^GETROWSNO()-2]);@INT("L_IFALLINGCOUNT",0); !GENERATING_CLONES_IF_NEEDED; @IF("ANNSTONE^GETCLONESNO()<L_ISTONECOUNT","{ANNSTONE^CLONE([L_ISTONECOUNT-ANNSTONE^GETCLONESNO()]);}","");@IF("BTNSTONE^GETCLONESNO()<L_ISTONECOUNT","{BTNSTONE^CLONE([L_ISTONECOUNT-BTNSTONE^GETCLONESNO()]);}",""); !SETTING_UP_STONES; @LOOP("{DTBSTONEDATA^SELECT(_I_);STRSTONEDATA^SET("DTBSTONEDATA_CURSOR");*["ANNSTONE_"+_I_]^SETPOSITION(STRSTONEDATA|POSX,STRSTONEDATA|POSY);*["ANNSTONE_"+_I_]^PLAY(STRSTONEDATA|INITEVT);*["BTNSTONE_"+_I_]^SETSTD(["ANNSTONE_"+_I_]);*["ANNSTONE_"+_I_]^SETPRIORITY(STRSTONEDATA|POSZ); !FALLING_ROCKS; @IF("STRSTONEDATA|HASFALL'TRUE","BEHADDFALLING","");}",1,[DTBSTONEDATA^GETROWSNO()-2],1);}

OBJECT=BEHADDFALLING
BEHADDFALLING:TYPE=BEHAVIOUR
BEHADDFALLING:CODE={L_IFALLINGCOUNT^INC();@IF("ANNFALLINGROCK^GETCLONESNO()<L_IFALLINGCOUNT","{ANNFALLINGROCK^CLONE(1);}","");ARRFALLING^SET(L_IFALLINGCOUNT,0,_I_);ARRFALLING^SET(L_IFALLINGCOUNT,1,STRSTONEDATA|FALLWAIT);*["ANNFALLINGROCK_"+L_IFALLINGCOUNT]^SETPOSITION(STRSTONEDATA|POSX,STRSTONEDATA|POSY);*["ANNFALLINGROCK_"+L_IFALLINGCOUNT]^PLAY("PAUSE");}

OBJECT=BEHBUBBLETROUBLE
BEHBUBBLETROUBLE:TYPE=BEHAVIOUR
BEHBUBBLETROUBLE:CODE={!GETTING_RID_OF_LEFTOVERS; @LOOP("{*["ANNBUBBLE_"+_I_]^STOP(FALSE);*["ANNBUBBLE_"+_I_]^HIDE();}",1,ANNBUBBLE^GETCLONESNO(),1); !MAIN_LOOP; @INT("L_IBUBBLECOUNT",0);@STRING("L_SFNAME",["BUBBLE"+SSTAGEID+[L_IBUBBLECOUNT+1]+".ANN"]);@WHILE("SYSTEM^ISFILEEXIST(L_SFNAME)","_",TRUE,"{L_IBUBBLECOUNT^INC();@IF("ANNBUBBLE^GETCLONESNO()<L_IBUBBLECOUNT","{ANNBUBBLE^CLONE(1);}","");*["ANNBUBBLE_"+L_IBUBBLECOUNT]^LOAD(L_SFNAME);*["ANNBUBBLE_"+L_IBUBBLECOUNT]^PLAY("PLAY");L_SFNAME^SET(["BUBBLE"+SSTAGEID+[L_IBUBBLECOUNT+1]+".ANN"]);}");}

OBJECT=BEHLOADSCENE
BEHLOADSCENE:TYPE=BEHAVIOUR
BEHLOADSCENE:CODE={CANVASOBSERVER^SETBACKGROUND(["BKG"+SSTAGEID+".IMG"]);IMGOVERLAY^LOAD(["OVERLAY"+SSTAGEID+".IMG"]);BEHBUBBLETROUBLE^RUN();BEHINITARRAYS^RUN();@IF("SSTAGEID'"A"","{BTNFINISH^SETRECT(710,470,800,600);}","");@IF("SSTAGEID'"B"","{BTNFINISH^SETRECT(710,470,800,600);}","");@IF("SSTAGEID'"C"","{BTNFINISH^SETRECT(577,461,800,600);}","");BEHGENERATESTONES^RUN();SNDSUCCESS^LOAD(["$WAVS\18SUCCESS"+SSTAGEID+".WAV"]);}

# ARGUMENTS: $1 - new stage ID
OBJECT=BEHCHANGESTAGE
BEHCHANGESTAGE:TYPE=BEHAVIOUR
BEHCHANGESTAGE:CODE={@STRING("L_NEWSTAGEID",$1);@IF("SSTAGEID!'L_NEWSTAGEID","{SSTAGEID^SET(L_NEWSTAGEID);BEHLOADSCENE^RUN();}","");BEHSTARTGAME^RUN();}

OBJECT=__INIT__
__INIT__:TYPE=BEHAVIOUR
__INIT__:CODE={BEHINITSCENEMUSIC^RUN(); BEHLOADSCENE^RUN();BEHSTARTGAME^RUN(); POZYCJAREKSIA^SET("WULKAN");POZYCJAWULKAN^SET(18);SAVEROZDZIAL6^SET("LAWAA");}

